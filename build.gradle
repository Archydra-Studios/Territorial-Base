plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'com.github.johnrengelman.shadow' version '5.2.0'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		name = "CottonMC"
		url = "https://server.bbkr.space/artifactory/libs-release"
	}
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}
	maven {
		name ="Siphalor"
		url = "https://maven.siphalor.de"
	}
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://jitpack.io/" }
}

loom {
	accessWidenerPath =  file("src/main/resources/territorial.accesswidener")
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// modImplementation(include("vazkii.patchouli:Patchouli:${project.patchouli_version}"))
	modImplementation(include("io.github.cottonmc:LibGui:${project.libgui_version}"))
	modApi(include("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"))
	modImplementation(include("de.siphalor:nbtcrafting-1.18:${project.nbtcrafing_version}"))
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"

	modImplementation "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
	modRuntimeOnly("com.github.astei:lazydfu:master-SNAPSHOT")

	// SQLite stuff
	//implementation(include("org.xerial:sqlite-jdbc:${project.jdbc_version}"))
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"

	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

shadowJar {
	configurations = [project.configurations.shadow]
	classifier = "shadow"
}

jar {
	from "LICENSE"
}
import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

task relocateShadowJar(type: ConfigureShadowRelocation) {
	target = tasks.shadowJar
	prefix = "${project.group}.${project.archivesBaseName}.shadow"
}
tasks.shadowJar.dependsOn tasks.relocateShadowJar

remapJar {
	dependsOn shadowJar
	input.set shadowJar.archiveFile.get()
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	repositories {
		mavenLocal()
	}
}